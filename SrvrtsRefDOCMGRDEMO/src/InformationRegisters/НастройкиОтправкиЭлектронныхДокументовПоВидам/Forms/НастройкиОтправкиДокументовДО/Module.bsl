
#Область ОписаниеПеременных

&НаКлиенте
Перем НастройкаРегламентаЭДО;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПравоНастройкиОбмена = ОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоНастройкиОбмена();
	ЕстьПравоОбработкиЭД     = ОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоОбработкиЭД();
	ЕстьПравоЧтенияЭД        = ОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоЧтенияЭД();
	
	ИспользуютсяДоговорыКонтрагентов = ОбменСКонтрагентамиПовтИсп.ИспользуютсяДоговорыКонтрагентов();
	
	ТолькоПросмотр = Не ЕстьПравоНастройкиОбмена;
	
	Элементы.ФормаОтправитьКаталогПоСоглашению.Видимость    = ЕстьПравоОбработкиЭД;
	Элементы.ФормаСформироватьСоглашениеПоШаблону.Видимость = ЕстьПравоОбработкиЭД;
	Элементы.ФормаОткрытьЭлектронныеДокументы.Видимость     = ЕстьПравоЧтенияЭД;
	Элементы.ДоговорКонтрагента.Видимость                   = ИспользуютсяДоговорыКонтрагентов;
	Элементы.ДекорацияСтатусПриглашения.Видимость           = ИспользуютсяДоговорыКонтрагентов;
	
	УстановитьУсловноеОформление();
	
	Элементы.ГруппаОжидание.Видимость = Ложь;
	Элементы.ГруппаОжиданиеУдаления.Видимость = Ложь;
	
	ПодготовитьФормуНаСервере(Параметры);
	
	УстановитьКлючНастройкиОтправки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		Если ЗавершениеРаботы Тогда
			ТекстПредупреждения = НСтр("ru = 'Настройки отправки не сохранены. Завершить работу с программой?'")
		Иначе
			Описание = Новый ОписаниеОповещения("ПередЗакрытиемОкнаФормы", ЭтотОбъект);
			ПоказатьВопрос(Описание, НСтр("ru = 'Настройки отправки изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "ОбновитьТекущиеДелаЭДО"
		Или ИмяСобытия = "ОбновитьСостояниеПриглашений")
		И НастройкаРегламентаЭДО <> Истина Тогда
		
		ПроверитьРасширенныйРежимИЗаполнитьСтатусыПриглашений();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УчетнаяЗаписьКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуВыбораУчетныхЗаписей();
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьОрганизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуВыбораУчетныхЗаписей();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ЗаполнитьИдентификаторыЭДО();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ЗаполнитьИдентификаторыЭДО();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПодключенияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ПустаяСтрока(ИдентификаторОтправителя)
		Или ПустаяСтрока(ИдентификаторПолучателя) Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Необходимо записать изменения в настройке отправки.
                             |Продолжить?'");
		
		Описание = Новый ОписаниеОповещения("ПослеВопросаНажатиеСтатусПодключения", ЭтотОбъект);
		ПоказатьВопрос(Описание, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ОткрытьПриглашение(ИдентификаторОтправителя, ИдентификаторПолучателя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("Организация, Контрагент", Организация, Контрагент);
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьФормуВыбораДоговора(
		ПараметрыФормы, Элемент, Неопределено, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыНастроекОтправки

&НаКлиенте
Процедура ИсходящиеДокументыДОВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьНастройкиОтправкиДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыДОПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	ВыполняемоеОповещение = Новый ОписаниеОповещения("НажатиеЗаписать", ЭтотОбъект, Новый Структура);
	ВыполняемоеОповещение.ДополнительныеПараметры.Вставить("ОповещениеКоманды", ВыполняемоеОповещение);
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ВыполняемоеОповещение = Новый ОписаниеОповещения("НажатиеЗаписатьИЗакрыть", ЭтотОбъект, Новый Структура);
	ВыполняемоеОповещение.ДополнительныеПараметры.Вставить("ОповещениеКоманды", ВыполняемоеОповещение);
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьКаталогПоСоглашению(Команда)
	ОбменСКонтрагентамиСлужебныйКлиент.ОтправитьКаталогТоваров(
		Организация, Контрагент, ДоговорКонтрагента, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭлектронныеДокументы(Команда)
	НастройкиЭДОСсылка = СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, ДоговорКонтрагента);
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьДеревоЭД(НастройкиЭДОСсылка, Неопределено, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСоглашениеПоШаблону(Команда)
	ОбменСКонтрагентамиСлужебныйКлиент.СформироватьСоглашениеПоШаблону(
		Организация, Контрагент, ДоговорКонтрагента);
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	УдалитьНастройкуОтправки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОформлениеФормы

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыДОНастройкиОтправкиПредставление.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсходящиеДокументыДО.Отправлять");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылкиБЭД);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыДОНастройкиОтправкиПредставление.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсходящиеДокументыДО.Отправлять");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.УчетнаяЗаписьКонтрагента.Видимость            = Истина;
	Элементы.УчетнаяЗаписьОрганизации.Видимость            = Истина;
	Элементы.СтатусПриглашения.Видимость                   = Истина;
	
	ДоступенВыборУчетныхЗаписей = ЗначениеЗаполнено(Форма.Организация) И ЗначениеЗаполнено(Форма.Контрагент);
	Элементы.УчетнаяЗаписьОрганизации.Доступность = ДоступенВыборУчетныхЗаписей;
	Элементы.УчетнаяЗаписьКонтрагента.Доступность = ДоступенВыборУчетныхЗаписей;
	
	Если ДоступенВыборУчетныхЗаписей Тогда
		ПодсказкаВводаУчетныхЗаписей = НСтр("ru = 'Выберите учетную запись'");
	Иначе 
		ПодсказкаВводаУчетныхЗаписей = "";
	КонецЕсли;
	Элементы.УчетнаяЗаписьОрганизации.ПодсказкаВвода = ПодсказкаВводаУчетныхЗаписей;
	Элементы.УчетнаяЗаписьКонтрагента.ПодсказкаВвода = ПодсказкаВводаУчетныхЗаписей;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьПредставлениеСтатусаПриглашения(СтатусПриглашений)
	
	
	Результат = Новый Структура("ТекстЗаголовка, ЦветСсылки");
	Результат.ТекстЗаголовка = Строка(СтатусПриглашений);
	Результат.ЦветСсылки = 0;
	
	Если СтатусПриглашений = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ПустаяСсылка")
		ИЛИ СтатусПриглашений = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяОтправить") Тогда
		Результат.ТекстЗаголовка = НСтр("ru = 'Отправить приглашение'");
	ИначеЕсли СтатусПриглашений = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.Отклонено")
			Или СтатусПриглашений = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.Ошибка") Тогда
			
			Результат.ЦветСсылки = 1;
			
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ИнициализацияФормы

&НаСервере
Процедура ЗаполнитьРеквизитыФормы(Знач ПараметрыФормы)
	ПараметрыФормы.Свойство("Организация" , Организация);
	ПараметрыФормы.Свойство("Контрагент"  , Контрагент);
	ПараметрыФормы.Свойство("Договор"     , ДоговорКонтрагента);
	
	Если ПараметрыФормы.Свойство("Ключ") Тогда
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Если ТипЗнч(ПараметрыФормы.Ключ) = Тип("СправочникСсылка.НастройкиЭДО") Тогда
				Организация = ПараметрыФормы.Ключ.Организация;
			Иначе
				Организация = ПараметрыФормы.Ключ.Отправитель;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Контрагент) Тогда
			Если ТипЗнч(ПараметрыФормы.Ключ) = Тип("СправочникСсылка.НастройкиЭДО") Тогда
				Контрагент = ПараметрыФормы.Ключ.Контрагент;
			Иначе
				Контрагент = ПараметрыФормы.Ключ.Получатель;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			Если ТипЗнч(ПараметрыФормы.Ключ) = Тип("СправочникСсылка.НастройкиЭДО") Тогда
				ДоговорКонтрагента = ПараметрыФормы.Ключ.ДоговорКонтрагента;
			Иначе
				ДоговорКонтрагента = ПараметрыФормы.Ключ.Договор;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	СоздатьПоДоговору = Неопределено;
	Создание          = Неопределено;
	Если ПараметрыФормы.Свойство("СоздатьПоДоговору"  , СоздатьПоДоговору) Тогда
		
		Элементы.ДоговорКонтрагента.ТолькоПросмотр = СоздатьПоДоговору = Ложь;
		Элементы.Организация.ТолькоПросмотр = ЗначениеЗаполнено(Организация);
		
	ИначеЕсли ПараметрыФормы.Свойство("Создание"  , Создание) И Создание Тогда
		
		Элементы.ДоговорКонтрагента.ТолькоПросмотр = Ложь;
		Элементы.Организация.ТолькоПросмотр        = Ложь;
		Элементы.Контрагент.ТолькоПросмотр         = Ложь;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере(ПараметрыФормы)
	
	ФорматыЭД.Загрузить(ОбменСКонтрагентамиСлужебный.ФорматыЭлектронныхДокументов());
	
	ТолькоПросмотр = Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам);
	
	ЗаполнитьРеквизитыФормы(ПараметрыФормы);
	
	ИспользуетсяНесколькоОрганизацийЭД = ЭлектронноеВзаимодействиеСлужебный.ИспользуетсяНесколькоОрганизаций();
	
	Если Не ИспользуетсяНесколькоОрганизацийЭД
		И НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = ЭлектронноеВзаимодействиеСлужебный.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ТаблицаНастроек = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНастройкиОтправкиДокументов();
	ТаблицаНастроек.Колонки.Добавить("ЭтоНовыйВидЭД", Новый ОписаниеТипов("Булево"));
	
	НастройкиОтправки = ОбменСКонтрагентамиСлужебный.ТекущиеНастройкиОтправкиЭлектронныхДокументовПоВидам(
		Организация, Контрагент, ДоговорКонтрагента, ТаблицаНастроек);
	
	ЗаполнитьПредставлениеНастроек(ЭтотОбъект, ТаблицаНастроек, НастройкиОтправки);
	ИсходящиеДокументы.Загрузить(ТаблицаНастроек);
	
	НастройкиОтправкиДО = РегистрыСведений.НастройкиОтправкиДокументовПоЭДО.НастройкиОтправкиДокументовПоЭДО(
		Организация, Контрагент);
	
	ИсходящиеДокументыДО.Загрузить(НастройкиОтправкиДО);
	
	ПроверитьРасширенныйРежимИЗаполнитьСтатусыПриглашений();
	
	Для Каждого СтрокаИсходящего Из ИсходящиеДокументыДО Цикл
		ЗаполнитьПредставлениеНастроекОтправкиВСтроке(СтрокаИсходящего);
	КонецЦикла;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеНастроек(Форма, НастройкиПоУмолчанию, Знач ТекущиеНастройки)
	
	СтрокаСЗаполненнымиДанными = Неопределено;
	Для Каждого ТекСтрока Из НастройкиПоУмолчанию Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("ВидДокумента"    , ТекСтрока.ВидДокумента);
		Отбор.Вставить("ПрикладнойВидЭД" , ТекСтрока.ПрикладнойВидЭД);
		
		СтрокиТаблицы = ТекущиеНастройки.НайтиСтроки(Отбор);
		
		Если СтрокиТаблицы.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(ТекСтрока, СтрокиТаблицы[0]);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекСтрока.ИдентификаторОтправителя) И ЗначениеЗаполнено(ТекСтрока.ИдентификаторПолучателя) Тогда
			СтрокаСЗаполненнымиДанными = ТекСтрока;
		КонецЕсли;
		ЗаполнитьПредставлениеРегламентаЭДОВСтроке(Форма, ТекСтрока);
		
	КонецЦикла;
	
	Если СтрокаСЗаполненнымиДанными <> Неопределено Тогда
		Отбор = Новый Структура("ИдентификаторОтправителя, ИдентификаторПолучателя", "", "");
		СтрокиСНезаполненнымиИдентификаторами = НастройкиПоУмолчанию.НайтиСтроки(Отбор);
		Для каждого СтрокаТЗ Из СтрокиСНезаполненнымиИдентификаторами Цикл
			ЗаполнитьЗначенияСвойств(СтрокаТЗ, СтрокаСЗаполненнымиДанными, "ИдентификаторОтправителя, ИдентификаторПолучателя, СпособОбменаЭД");
		КонецЦикла;
	КонецЕсли;
	
	НастройкиПоУмолчанию.Сортировать("Приоритет");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтатусПриглашения(Знач ИдентификаторОтправителя, Знач ИдентификаторПолучателя)
	
	СтатусПодключения = Перечисления.СтатусыПриглашений.ТребуетсяОтправить;
	
	ДанныеПриглашения = РегистрыСведений.ПриглашенияКОбменуЭлектроннымиДокументами.ДанныеПриглашения(
							ИдентификаторОтправителя, ИдентификаторПолучателя);
							
	Если ЗначениеЗаполнено(ДанныеПриглашения) Тогда
		СтатусПодключения = ДанныеПриглашения.Статус;
	КонецЕсли;
	
	Возврат СформироватьПредставлениеСтатусаПриглашения(СтатусПодключения);
	
КонецФункции

#КонецОбласти

#Область РедактированиеУчетныхЗаписей

&НаКлиенте
Процедура ОткрытьФормуВыбораУчетныхЗаписей()
	
	Если Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ПриЗавершенииВыбораУченыхЗаписей", ЭтотОбъект);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Отправитель"             , Организация);
	ПараметрыОткрытия.Вставить("Получатель"              , Контрагент);
	ПараметрыОткрытия.Вставить("ДоговорКонтрагента"      , ДоговорКонтрагента);
	ПараметрыОткрытия.Вставить("ИдентификаторПолучателя" , ИдентификаторПолучателя);
	ПараметрыОткрытия.Вставить("ИдентификаторОтправителя", ИдентификаторОтправителя);
	ПараметрыОткрытия.Вставить("ВыборУчетныхЗаписей"     , Истина);
	
	ОткрытьФорму("Обработка.ОбменСКонтрагентами.Форма.НастройкаОбменаСКонтрагентом", ПараметрыОткрытия,
		УникальныйИдентификатор,,,, Описание, режимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступныеИдентификаторы()
	
	Элементы.УчетнаяЗаписьОрганизации.СписокВыбора.Очистить();
	Элементы.УчетнаяЗаписьКонтрагента.СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Контрагент) Тогда
		ДоступныеИдентификаторы = ОбменСКонтрагентамиСлужебный.ДоступныеИдентификаторыЭДО(Организация, Контрагент);
		
		Для Каждого Идентификатор Из ДоступныеИдентификаторы.ИдентификаторыОтправителя Цикл
			
			Элементы.УчетнаяЗаписьОрганизации.СписокВыбора.Добавить(Идентификатор.Значение, Идентификатор.Представление);
			
		КонецЦикла;
		
		Для Каждого Идентификатор Из ДоступныеИдентификаторы.ИдентификаторыПолучателя Цикл
			
			Элементы.УчетнаяЗаписьКонтрагента.СписокВыбора.Добавить(Идентификатор.Значение, Идентификатор.Представление);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииВыбораУченыхЗаписей(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДанныеПриИзмененииИдентификаторов(Результат);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПриИзмененииИдентификаторов(Результат)
	
	ИдентификаторОтправителя = ВРег(Результат.ИдентификаторОрганизации);
	ИдентификаторПолучателя  = ВРег(Результат.ИдентификаторКонтрагента);
	СпособОбмена             = ПолучитьСпособОбменаУчетнойЗаписи(ИдентификаторОтправителя);
	
	Таблица = ИсходящиеДокументы.Выгрузить();
	Таблица.ЗаполнитьЗначения(ИдентификаторОтправителя, "ИдентификаторОтправителя");
	Таблица.ЗаполнитьЗначения(ИдентификаторПолучателя , "ИдентификаторПолучателя");
	Таблица.ЗаполнитьЗначения(СпособОбмена,             "СпособОбменаЭД");
	ИсходящиеДокументы.Загрузить(Таблица);
	
	ОбновитьФорматыКонтрагента();
	ЗаполнитьПризнакВыгружатьДополнительныеСведения();
	СтруктураСтатусПодключения = ПолучитьСтатусПриглашения(ИдентификаторОтправителя, ИдентификаторПолучателя);
	СтатусПриглашения = СтруктураСтатусПодключения.ТекстЗаголовка;
	
	Если СтруктураСтатусПодключения.ЦветСсылки = 0 Тогда
		Элементы.СтатусПриглашения.ЦветТекста = ЦветаСтиля.ЦветГиперссылкиБЭД;
	ИначеЕсли СтруктураСтатусПодключения.ЦветСсылки = 1 Тогда
		Элементы.СтатусПриглашения.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ПодборПодходящихИдентификаторовЭДО

&НаКлиенте
Процедура ЗаполнитьИдентификаторыЭДО()
	
	ОбновитьДоступныеИдентификаторы();
	
	ИдентификаторыОтправителя = Элементы.УчетнаяЗаписьОрганизации.СписокВыбора.ВыгрузитьЗначения();
	ИдентификаторыПолучателя  = Элементы.УчетнаяЗаписьКонтрагента.СписокВыбора.ВыгрузитьЗначения();
	
	Если ЗначениеЗаполнено(ИдентификаторыОтправителя) И ЗначениеЗаполнено(ИдентификаторыПолучателя) Тогда
		Элементы.ДекорацияНадпись.Заголовок = НСтр("ru = 'Ожидается получение данных по совместимости идентификаторов ЭДО'");
		Элементы.ГруппаОжидание.Видимость = Истина;
		
		Оповещение = Новый ОписаниеОповещения("ПодобратьПодходящиеИдентификаторыЭДОЗавершение", ЭтотОбъект);
		
		ПараметрыПроцедуры = Новый Структура;
		ПараметрыПроцедуры.Вставить("ИдентификаторыОтправителя", ИдентификаторыОтправителя);
		ПараметрыПроцедуры.Вставить("ИдентификаторыПолучателя",  ИдентификаторыПолучателя);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительнаяОперация = ПодобратьПодходящиеИдентификаторыЭДО(ПараметрыПроцедуры, УникальныйИдентификатор);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодобратьПодходящиеИдентификаторыЭДО(Знач ПараметрыПроцедуры,Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подбор подходящих идентификаторов ЭДО'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ОбменСКонтрагентамиСлужебный.ПолучитьЛучшуюПоРейтингуСвязкуИдентификаторовЭДО", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПодобратьПодходящиеИдентификаторыЭДОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		УстановитьИдентификаторыЭДО(Результат.АдресРезультата);
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Во время выбора подходящих учетных записей произошла ошибка.
                                                                |Подробнее см. в журнале регистрации.'"));
		
	КонецЕсли;
	
	Элементы.ГруппаОжидание.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИдентификаторыЭДО(АдресРезультата)
	
	Идентификаторы = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если Не ЗначениеЗаполнено(Идентификаторы) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не найден идентификатор Абонента ЭДО'"));
		Возврат;
	КонецЕсли;
	
	ИдентификаторОтправителя = Врег(Идентификаторы.ИдентификаторОтправителя);
	ИдентификаторПолучателя = ВРег(Идентификаторы.ИдентификаторПолучателя);
	СпособОбменаЭД = ПолучитьСпособОбменаУчетнойЗаписи(ИдентификаторОтправителя);
	
	Таблица = ИсходящиеДокументы.Выгрузить();
	Таблица.ЗаполнитьЗначения(ИдентификаторОтправителя, "ИдентификаторОтправителя");
	Таблица.ЗаполнитьЗначения(ИдентификаторПолучателя , "ИдентификаторПолучателя");
	Таблица.ЗаполнитьЗначения(СпособОбменаЭД          , "СпособОбменаЭД");
	ИсходящиеДокументы.Загрузить(Таблица);
	
	ОбновитьФорматыКонтрагента();
	ЗаполнитьПризнакВыгружатьДополнительныеСведения();
	ОбновитьДоступныеИдентификаторы();
	
	ПроверитьРасширенныйРежимИЗаполнитьСтатусыПриглашений();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиОтправкиДокументовДО

&НаКлиенте
Процедура ОткрытьНастройкиОтправкиДО()
	
	ТекущиеДанные = Элементы.ИсходящиеДокументыДО.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТекущиеДанные.Отправлять Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ИДСтроки", ТекущиеДанные.ПолучитьИдентификатор());
	
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыНастроекОтправки", ЭтотОбъект,
		ПараметрыОбработчика);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидЭД", ТекущиеДанные.ВидЭД);
	ПараметрыФормы.Вставить("ТипЭД", ТекущиеДанные.ТипЭД);
	ПараметрыФормы.Вставить("ТребуетсяОтветнаяПодпись", ТекущиеДанные.ТребуетсяОтветнаяПодпись);
	ПараметрыФормы.Вставить("ТребуетсяИзвещениеОПолучении", ТекущиеДанные.ТребуетсяИзвещениеОПолучении);
	
	ОткрытьФорму("Обработка.ОбменСКонтрагентами.Форма.НастройкаОтправкиДокументовДО", ПараметрыФормы,
		ЭтаФорма, , , , ОбработчикЗакрытия, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыНастроекОтправки(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаНастроек = ИсходящиеДокументыДО.НайтиПоИдентификатору(
		ДополнительныеПараметры.ИДСтроки);
	
	Если СтрокаНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из Результат Цикл
		Если Элемент.Значение <> СтрокаНастроек[Элемент.Ключ] Тогда
			Модифицированность = Истина;
			СтрокаНастроек[Элемент.Ключ] = Элемент.Значение;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьПредставлениеНастроекОтправкиВСтроке(СтрокаНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура НажатиеЗаписать(ПропуститьПроверки, ДополнительныеПараметры) Экспорт

	Модифицированность = НЕ НачатьСохраненияНастроек(ПропуститьПроверки, ДополнительныеПараметры.ОповещениеКоманды);

КонецПроцедуры

&НаКлиенте
Процедура НажатиеЗаписатьИЗакрыть(ПропуститьПроверки, ДополнительныеПараметры) Экспорт

	Если НачатьСохраненияНастроек(ПропуститьПроверки, ДополнительныеПараметры.ОповещениеКоманды) Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаНажатиеСтатусПодключения(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Записать(Неопределено);
	Иначе
		Возврат;
	КонецЕсли;
	
	ОткрытьПриглашение(ИдентификаторОтправителя, ИдентификаторПолучателя);
	
КонецПроцедуры

&НаКлиенте
Функция НачатьСохраненияНастроек(ПропуститьПроверки = Ложь, ОповещениеКоманды = Неопределено)
	
	Если Не ПропуститьПроверки Тогда
		// Выполняем серверный вызов, так как нужно проверить валидность маршрута и в случае ошибок спросить у пользователя,
		// записывать ли его.
		ЕстьОшибкиЗаполнения = Ложь;
		КонтекстОперации = ЭлектронноеВзаимодействиеСлужебныйКлиент.НовыйКонтекстОперации();
		Если Не НастройкаВалидирована(ЕстьОшибкиЗаполнения, КонтекстОперации) Тогда
			Если Не ЕстьОшибкиЗаполнения Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения("ВопросОЗаписиПолученОтвет", ЭтотОбъект,
					Новый Структура("ОповещениеКоманды, КонтекстОперации", ОповещениеКоманды, КонтекстОперации));
				ТекстВопроса = НСтр("ru = 'Обнаружены возможные ошибки в настройках маршрутов. Продолжить запись?'");
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет, 
					НСтр("ru = 'Настройка некорректна'"));
			КонецЕсли;
				
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Результат = ЗаписатьНастройки(Организация, Контрагент, ДоговорКонтрагента, ИсходящиеДокументы, ИсходящиеДокументыДО);
	
	Если Результат Тогда
		Модифицированность = Ложь;
		УстановитьКлючНастройкиОтправки(ЭтотОбъект);
		Оповестить("ОбновитьТекущиеДелаЭДО", Контрагент, Организация);
		ТекстЗаголовка = НСтр("ru = 'Успех'");
		ТекстСообщения = НСтр("ru = 'Настройки отправки сохранены успешно'");
		ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ВопросОЗаписиПолученОтвет(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОчиститьСообщения();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеКоманды, Истина);
	Иначе 
		ЭлектронноеВзаимодействиеОбработкаОшибокКлиент.ОбработатьОшибки(ДополнительныеПараметры.КонтекстОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НастройкаВалидирована(ЕстьОшибкиЗаполнения = Ложь, КонтекстОперации = Неопределено)
	Если НЕ ПроверитьЗаполнение() Тогда
		ЕстьОшибкиЗаполнения = Истина;	
	КонецЕсли;
	
	ЕстьОшибкиВЗависимыхНастройках = Ложь;
	Если Не ЕстьОшибкиЗаполнения Тогда
		// Проверим, что все указанные маршруты возможно выполнить
		ПроверитьСоответствиеСертификатовМаршрутам(ЕстьОшибкиВЗависимыхНастройках, КонтекстОперации);
	КонецЕсли;
	
	Возврат НЕ ЕстьОшибкиЗаполнения И Не ЕстьОшибкиВЗависимыхНастройках;

КонецФункции

&НаСервере
Процедура ПроверитьСоответствиеСертификатовМаршрутам(Отказ, КонтекстОперации)
	
	// Подготовим соответствие наборов сертификатов учетным записям
	ИспользованныеУчетныеЗаписи = ИсходящиеДокументы.Выгрузить(, "ИдентификаторОтправителя").ВыгрузитьКолонку("ИдентификаторОтправителя");
	
	НаборыСертификатовУчетныхЗаписей = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчетныеЗаписи", ИспользованныеУчетныеЗаписи);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО КАК УчетнаяЗапись,
	|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
	|ИЗ
	|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
	|ГДЕ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО В(&УчетныеЗаписи)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчетнаяЗапись,
	|	Сертификат
	|ИТОГИ ПО
	|	УчетнаяЗапись";
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаУчетныхЗаписей = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	УстановитьПривилегированныйРежим(Ложь);
	Пока ВыборкаУчетныхЗаписей.Следующий() Цикл
		СертификатыУчетныхЗаписей = Новый Массив;
		ИдентификаторНабора = "ИД_";
		Выборка = ВыборкаУчетныхЗаписей.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если СертификатыУчетныхЗаписей.Найти(Выборка.Сертификат) = Неопределено Тогда
				СертификатыУчетныхЗаписей.Добавить(Выборка.Сертификат);
				ИдентификаторНабора = ИдентификаторНабора + Строка(Выборка.Сертификат.УникальныйИдентификатор());
			КонецЕсли; 
		КонецЦикла;
		
		ИдентификаторНабора = СтрЗаменить(ИдентификаторНабора, "-", "_");
		СтруктураОписанияНабораСертификатов = Новый Структура("ИдентификаторНабора, Сертификаты", 
			ИдентификаторНабора, СертификатыУчетныхЗаписей);
		НаборыСертификатовУчетныхЗаписей.Вставить(ВыборкаУчетныхЗаписей.УчетнаяЗапись, СтруктураОписанияНабораСертификатов);
	КонецЦикла;
	
	// Подготовим уникальные комбинации значений наборов сертификатов, маршрутов и видов ЭД.
	УникальныеПараметрыПроверки = Новый Структура;
	Для Каждого СтрокаИсходящегоДокумента Из ИсходящиеДокументы Цикл
		Если СтрокаИсходящегоДокумента.Формировать Тогда
			МаршрутПодписания 	     = СтрокаИсходящегоДокумента.МаршрутПодписания;
			ВидЭД				     = СтрокаИсходящегоДокумента.ВидДокумента;
			УчетнаяЗаписьНастроекЭДО = СтрокаИсходящегоДокумента.ИдентификаторОтправителя;
			
			ПараметрыНабораСертификатов = НаборыСертификатовУчетныхЗаписей[ИдентификаторОтправителя];
			Если ПараметрыНабораСертификатов <> Неопределено Тогда
				ИдентификаторМаршрута   = СтрЗаменить(Строка(МаршрутПодписания.УникальныйИдентификатор()), "-", "_");
				ИдентификаторПараметров = ПараметрыНабораСертификатов.ИдентификаторНабора + Строка(ИдентификаторМаршрута);
				
				СтруктураОписанияПараметров = Неопределено;
				Если Не УникальныеПараметрыПроверки.Свойство(ИдентификаторПараметров, СтруктураОписанияПараметров) Тогда
					СтруктураОписанияПараметров = Новый Структура;
					СтруктураОписанияПараметров.Вставить("Сертификаты",       ПараметрыНабораСертификатов.Сертификаты);
					СтруктураОписанияПараметров.Вставить("МаршрутПодписания", МаршрутПодписания);
					СтруктураОписанияПараметров.Вставить("ВидыЭД",            Новый Массив);
					УникальныеПараметрыПроверки.Вставить(ИдентификаторПараметров, СтруктураОписанияПараметров);
				КонецЕсли;
				СтруктураОписанияПараметров.ВидыЭД.Добавить(ВидЭД);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеНастройки = Новый Структура("Отправитель, Получатель, Договор");
	ОписаниеНастройки.Отправитель = Организация;
	ОписаниеНастройки.Получатель  = Контрагент;
	ОписаниеНастройки.Договор     = ДоговорКонтрагента;
	НастройкаОбмена = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументов.СоздатьКлючЗаписи(ОписаниеНастройки);
	ПредставлениеНастройки = ОбменСКонтрагентамиСлужебный.ПредставлениеНастройкиОтправкиЭлектронныхДокументов(ОписаниеНастройки);
	
	// Проверим валидность каждого набора параметров проверки
	Для Каждого НаборПараметровПроверки Из УникальныеПараметрыПроверки Цикл
		МаршрутПодписания = НаборПараметровПроверки.Значение.МаршрутПодписания;
		Сертификаты       = НаборПараметровПроверки.Значение.Сертификаты;
		ВидыЭД            = НаборПараметровПроверки.Значение.ВидыЭД;
		
		РезультатыПроверки = ЭлектронноеВзаимодействиеСлужебный.РезультатыПроверкиМаршрутаПоПараметрамНастройки(
			МаршрутПодписания, Сертификаты, ВидыЭД);
			
		ЭлектронноеВзаимодействиеСлужебный.ВывестиРезультатыПроверкиМаршрута(РезультатыПроверки, 
			НастройкаОбмена, МаршрутПодписания, Отказ,, ПредставлениеНастройки, КонтекстОперации);
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаписатьНастройки(Знач Организация, Знач Контрагент, Знач ДоговорКонтрагента, Знач ИсходящиеДокументы, Знач ИсходящиеДокументыДО)
	
	ИсходящиеДокументы = ИсходящиеДокументы.Выгрузить();
	
	ИсходящиеДокументы.Колонки.Добавить("Отправитель");
	ИсходящиеДокументы.ЗаполнитьЗначения(Организация, "Отправитель");
	
	ИсходящиеДокументы.Колонки.Добавить("Получатель");
	ИсходящиеДокументы.ЗаполнитьЗначения(Контрагент, "Получатель");
	
	ИсходящиеДокументы.Колонки.Добавить("Договор");
	ИсходящиеДокументы.ЗаполнитьЗначения(ДоговорКонтрагента, "Договор");
	
	// Подготовим таблицу уникальных пар идентификаторов для создания приглашений.
	Идентификаторы = ИсходящиеДокументы.Скопировать(, "ИдентификаторОтправителя, ИдентификаторПолучателя");
	Идентификаторы.Свернуть("ИдентификаторОтправителя, ИдентификаторПолучателя");
	ИдентификаторыОтфильтрованные = Идентификаторы.СкопироватьКолонки();
	Для Каждого СтрокаИдентификатора Из Идентификаторы Цикл
		Если ЗначениеЗаполнено(СтрокаИдентификатора.ИдентификаторОтправителя)
			И ЗначениеЗаполнено(СтрокаИдентификатора.ИдентификаторПолучателя) Тогда
		
			НоваяСтрока = ИдентификаторыОтфильтрованные.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИдентификатора);
		КонецЕсли;
	КонецЦикла;
	
	ИсходящиеДокументыДО = ИсходящиеДокументыДО.Выгрузить();
	
	ИсходящиеДокументыДО.Колонки.Добавить("Отправитель");
	ИсходящиеДокументыДО.ЗаполнитьЗначения(Организация, "Отправитель");
	
	ИсходящиеДокументыДО.Колонки.Добавить("Получатель");
	ИсходящиеДокументыДО.ЗаполнитьЗначения(Контрагент, "Получатель");
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам");
		ЭлементБлокировкиДанных.УстановитьЗначение("Отправитель", Организация);
		ЭлементБлокировкиДанных.УстановитьЗначение("Получатель" , Контрагент);
		ЭлементБлокировкиДанных.УстановитьЗначение("Договор"    , ДоговорКонтрагента);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументов");
		ЭлементБлокировкиДанных.УстановитьЗначение("Отправитель", Организация);
		ЭлементБлокировкиДанных.УстановитьЗначение("Получатель" , Контрагент);
		ЭлементБлокировкиДанных.УстановитьЗначение("Договор"    , ДоговорКонтрагента);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.НастройкиОтправкиДокументовПоЭДО");
		ЭлементБлокировкиДанных.УстановитьЗначение("Отправитель", Организация);
		ЭлементБлокировкиДанных.УстановитьЗначение("Получатель" , Контрагент);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		
		БлокировкаДанных.Заблокировать();
		
		Если ИдентификаторыОтфильтрованные.Количество() Тогда
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами");
			ЭлементБлокировкиДанных.ИсточникДанных = ИдентификаторыОтфильтрованные;
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("ИдентификаторОрганизации", "ИдентификаторОтправителя");
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("ИдентификаторКонтрагента", "ИдентификаторПолучателя");
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Идентификаторы", ИдентификаторыОтфильтрованные);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	Идентификаторы.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
			|	Идентификаторы.ИдентификаторПолучателя КАК ИдентификаторПолучателя
			|ПОМЕСТИТЬ Идентификаторы
			|ИЗ
			|	&Идентификаторы КАК Идентификаторы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Идентификаторы.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
			|	Идентификаторы.ИдентификаторПолучателя КАК ИдентификаторПолучателя
			|ИЗ
			|	Идентификаторы КАК Идентификаторы
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
			|		ПО Идентификаторы.ИдентификаторОтправителя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации
			|			И Идентификаторы.ИдентификаторПолучателя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента
			|ГДЕ
			|	ПриглашенияКОбменуЭлектроннымиДокументами.Статус ЕСТЬ NULL";
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				РегистрыСведений.ПриглашенияКОбменуЭлектроннымиДокументами.НовыйСтатусПриглашения(Выборка.ИдентификаторОтправителя,
					Выборка.ИдентификаторПолучателя, Перечисления.СтатусыПриглашений.ТребуетсяОтправить, Контрагент,
					ОбменСКонтрагентамиСлужебный.СтандартныйШаблонПриглашения());
			КонецЦикла;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Отправитель.Установить(Организация);
		НаборЗаписей.Отбор.Получатель.Установить(Контрагент);
		НаборЗаписей.Отбор.Договор.Установить(ДоговорКонтрагента);
		НаборЗаписей.Загрузить(ИсходящиеДокументы);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументов.СоздатьМенеджерЗаписи();
		НаборЗаписей.Отправитель = Организация;
		НаборЗаписей.Получатель  = Контрагент;
		НаборЗаписей.Договор = ДоговорКонтрагента;
		НаборЗаписей.Прочитать();
		НаборЗаписей.ИспользоватьУПД = Истина;
		НаборЗаписей.ИспользоватьУКД = Истина;
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.НастройкиОтправкиДокументовПоЭДО.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Отправитель.Установить(Организация);
		НаборЗаписей.Отбор.Получатель.Установить(Контрагент);
		НаборЗаписей.Загрузить(ИсходящиеДокументыДО);
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ВидОперации = НСтр("ru = 'Сохранение настроек отправки ЭДО'");
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке),
			НСтр("ru = 'Не удалось сохранить Настройки отправки
                  |Подробнее см. в журнале регистрации.'"));
			
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
КонецФункции

&НаСервереБезКонтекста
Функция СсылкаНаОбъектНастройкиЭДО(Знач Организация, Знач Контрагент, Знач ДоговорКонтрагента)
	Возврат ОбменСКонтрагентамиСлужебный.СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, ДоговорКонтрагента);
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемОкнаФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИЗакрыть(Элементы.ФормаЗаписатьИЗакрыть);
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСпособОбменаУчетнойЗаписи(Знач ИдентификаторОтправителя)
	
	Данные = РегистрыСведений.УчетныеЗаписиЭДО.ДанныеУчетнойЗаписиЭДОПоИдентификатору(ИдентификаторОтправителя);
	
	Если ЗначениеЗаполнено(Данные) Тогда
		Возврат Данные.СпособОбменаЭД;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПроверитьРасширенныйРежимИЗаполнитьСтатусыПриглашений()

	Таблица = ИсходящиеДокументы.Выгрузить(, "ИдентификаторОтправителя, ИдентификаторПолучателя");
	Таблица.Свернуть("ИдентификаторОтправителя, ИдентификаторПолучателя");
	
	ИдентификаторОтправителя = ВРег(Таблица[0].ИдентификаторОтправителя);
	ИдентификаторПолучателя  = ВРег(Таблица[0].ИдентификаторПолучателя);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаИдентификаторы.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
		|	ТаблицаИдентификаторы.ИдентификаторПолучателя КАК ИдентификаторПолучателя
		|ПОМЕСТИТЬ ТаблицаИдентификаторы
		|ИЗ
		|	&Идентификаторы КАК ТаблицаИдентификаторы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИдентификаторы.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
		|	ТаблицаИдентификаторы.ИдентификаторПолучателя КАК ИдентификаторПолучателя,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.Статус КАК СтатусПриглашения,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.ОператорЭДО КАК ОператорЭДО
		|ИЗ
		|	ТаблицаИдентификаторы КАК ТаблицаИдентификаторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
		|		ПО ТаблицаИдентификаторы.ИдентификаторОтправителя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации
		|			И ТаблицаИдентификаторы.ИдентификаторПолучателя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента";
	
	Запрос.УстановитьПараметр("Идентификаторы", Таблица);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТаблицаИсходящиеДокументы = ИсходящиеДокументы.Выгрузить();
	ТаблицаИсходящиеДокументы.ЗаполнитьЗначения(Перечисления.СтатусыПриглашений.ПустаяСсылка(), "СтатусПриглашения");
	ТаблицаИсходящиеДокументы.Индексы.Добавить("ИдентификаторОтправителя, ИдентификаторПолучателя");
	
	ИмяПоляСтатус = "СтатусПриглашения";
	ИсходныеДанные = Новый ТаблицаЗначений;
	ИсходныеДанные.Колонки.Добавить("ИдентификаторОрганизации");
	ИсходныеДанные.Колонки.Добавить("ИдентификаторКонтрагента");
	ИсходныеДанные.Колонки.Добавить("ОператорЭДО");
	ИсходныеДанные.Колонки.Добавить(ИмяПоляСтатус);
	ИсходныеДанные.Колонки.Добавить("Оформление");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Отбор = Новый Структура();
		Отбор.Вставить("ИдентификаторОтправителя", ВыборкаДетальныеЗаписи.ИдентификаторОтправителя);
		Отбор.Вставить("ИдентификаторПолучателя" , ВыборкаДетальныеЗаписи.ИдентификаторПолучателя);
		
		НайденныеСтроки = ТаблицаИсходящиеДокументы.НайтиСтроки(Отбор);
		
		Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
			
			СтрокаТЧ.СтатусПриглашения = ВыборкаДетальныеЗаписи.СтатусПриглашения;
			
			СтруктураСтатусПодключения = СформироватьПредставлениеСтатусаПриглашения(СтрокаТЧ.СтатусПриглашения);
			
			СтрокаТЧ.СтатусПриглашенияПредставление = СтруктураСтатусПодключения.ТекстЗаголовка;
			СтрокаТЧ.ЦветГиперссылки = СтруктураСтатусПодключения.ЦветСсылки;
			
			НоваяСтрока = ИсходныеДанные.Добавить();
			НоваяСтрока.ИдентификаторОрганизации = ВыборкаДетальныеЗаписи.ИдентификаторОтправителя;
			НоваяСтрока.ИдентификаторКонтрагента = ВыборкаДетальныеЗаписи.ИдентификаторПолучателя;
			НоваяСтрока.ОператорЭДО = ВыборкаДетальныеЗаписи.ОператорЭДО;
			НоваяСтрока[ИмяПоляСтатус] = СтрокаТЧ.СтатусПриглашения;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОбменСКонтрагентамиСлужебный.ЗаменитьПредставлениеСтатусовПриглашений(ИсходныеДанные, ИмяПоляСтатус);
	
	ИсходныеДанные.Свернуть(СтрШаблон("ИдентификаторОрганизации, ИдентификаторКонтрагента, %1", ИмяПоляСтатус));
	
	Для каждого СтрокаТаблицы Из ТаблицаИсходящиеДокументы Цикл
		Отбор = Новый Структура();
		Отбор.Вставить("ИдентификаторОрганизации", СтрокаТаблицы.ИдентификаторОтправителя);
		Отбор.Вставить("ИдентификаторКонтрагента", СтрокаТаблицы.ИдентификаторПолучателя);
		СтрокиИсходныхДанных = ИсходныеДанные.НайтиСтроки(Отбор);
		Если СтрокиИсходныхДанных.Количество() Тогда
			СтрокаТаблицы.СтатусПриглашенияПредставление = СтрокиИсходныхДанных[0][ИмяПоляСтатус];
		КонецЕсли;
	КонецЦикла;
	
	ИсходящиеДокументы.Загрузить(ТаблицаИсходящиеДокументы);
	
	Если Таблица.Количество() = 1 Тогда
		
		ОбновитьДоступныеИдентификаторы();
		
		СтатусПриглашения = ТаблицаИсходящиеДокументы[0].СтатусПриглашенияПредставление;
		
		Если ИсходящиеДокументы[0].ЦветГиперссылки = 0 Тогда
			Элементы.СтатусПриглашения.ЦветТекста = ЦветаСтиля.ЦветГиперссылкиБЭД;
		ИначеЕсли ИсходящиеДокументы[0].ЦветГиперссылки = 1 Тогда
			Элементы.СтатусПриглашения.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
		КонецЕсли;
		
	КонецЕсли;

	Возврат Таблица.Количество() > 1;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеРегламентаЭДОВСтроке(Форма, СтрокаТаблицы)
	
	Строки = Форма.ФорматыЭД.НайтиСтроки(Новый Структура("ИдентификаторФормата", СтрокаТаблицы.ВерсияФормата));
	
	МассивСтрок = Новый Массив;
	Если Строки.Количество() > 0 Тогда
		МассивСтрок.Добавить(Строки[0].ПредставлениеФормата);
	Иначе
		МассивСтрок.Добавить(НСтр("ru = 'Произвольный документ'"));
	КонецЕсли;
	
	МассивСтрок.Добавить(?(СтрокаТаблицы.ТребуетсяОтветнаяПодпись, НСтр("ru = 'с ответной подписью'"), НСтр("ru = 'без ответной подписи'")));
	
	СтрокаТаблицы.ДополнительныеНастройки = СтрСоединить(МассивСтрок,", ");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПриглашение(ВыбранныйИдентификаторОрганизации, ВыбранныйИдентификаторКонтрагента)
	
	Если Не ЗначениеЗаполнено(Организация)
		Или Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	КлючЗаписи = ОбменСКонтрагентамиСлужебныйКлиент.КлючЗаписиПриглашения(
		ВыбранныйИдентификаторОрганизации, ВыбранныйИдентификаторКонтрагента);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("Контрагент",  Контрагент);
	ОткрытьФорму("РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами.ФормаЗаписи",
		ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКлючНастройкиОтправки(Форма)
	Форма.КлючНастройкиОтправки = Новый ФиксированнаяСтруктура("Отправитель, Получатель, Договор",
		Форма.Организация, Форма.Контрагент, Форма.ДоговорКонтрагента);
КонецПроцедуры

&НаКлиенте
Функция КлючНастройкиОтправкиПустой()
	Возврат НЕ (ЗначениеЗаполнено(КлючНастройкиОтправки.Отправитель)
		ИЛИ ЗначениеЗаполнено(КлючНастройкиОтправки.Получатель)
		ИЛИ ЗначениеЗаполнено(КлючНастройкиОтправки.Договор));
КонецФункции

&НаСервере
Процедура ОбновитьФорматыКонтрагента()
	
	Если Не ЗначениеЗаполнено(ИдентификаторПолучателя) Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = ИсходящиеДокументы.Выгрузить();
	Настройки.Свернуть("ИдентификаторПолучателя");
	НаборИдентификаторовКонтрагентов = Настройки.ВыгрузитьКолонку("ИдентификаторПолучателя");
	
	ПоддерживаемыеФорматы = ОбменСКонтрагентамиСлужебный.ПоддерживаемыеФорматыКонтрагента(НаборИдентификаторовКонтрагентов);
	
	Для каждого СтрокаНастроек Из ИсходящиеДокументы Цикл
		
		Если СтрокаНастроек.ВерсияФорматаУстановленаВручную Тогда
			Продолжить;
		КонецЕсли;
		
		ОтборСтрок = Новый Структура("ИдентификаторКонтрагента,ВидЭД", СтрокаНастроек.ИдентификаторПолучателя, СтрокаНастроек.ВидДокумента);
		НайденныеСтроки = ПоддерживаемыеФорматы.НайтиСтроки(ОтборСтрок);
		Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаФормата = НайденныеСтроки[0];
		Если СтрокаФормата <> Неопределено И СтрокаФормата.Формат <> СтрокаНастроек.ВерсияФормата Тогда
			СтрокаНастроек.ВерсияФормата = СтрокаФормата.Формат;
			ЗаполнитьПредставлениеРегламентаЭДОВСтроке(ЭтотОбъект, СтрокаНастроек);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеНастроекОтправкиВСтроке(СтрокаТаблицы)
	
	СтрокиНастроек = Новый Массив;
	
	ПустойВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ПустаяСсылка");
	ПроизвольныйВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ПроизвольныйЭД");
	ПрочийТипЭД = ПредопределенноеЗначение("Перечисление.ТипыЭД.Прочее");
	
	Если СтрокаТаблицы.ВидЭД = ПустойВидЭД Тогда
		
		СтрокиНастроек.Добавить(НСтр("ru = 'Автоматически'"));
		
		Если СтрокаТаблицы.ТипЭД <> ПрочийТипЭД Тогда
			СтрокиНастроек.Добавить(СтрокаТаблицы.ТипЭД);
		КонецЕсли;
		
	ИначеЕсли СтрокаТаблицы.ВидЭД = ПроизвольныйВидЭД Тогда
		
		СтрокиНастроек.Добавить(СтрокаТаблицы.ВидЭД);
		СтрокиНастроек.Добавить(СтрокаТаблицы.ТипЭД);
		
		Если СтрокаТаблицы.ТребуетсяОтветнаяПодпись Тогда
			СтрокиНастроек.Добавить(НСтр("ru = 'с ответной подписью'"));
		Иначе
			СтрокиНастроек.Добавить(НСтр("ru = 'без ответной подписи'"));
		КонецЕсли;
		
	Иначе
		
		СтрокиНастроек.Добавить(СтрокаТаблицы.ВидЭД);
		
	КонецЕсли;
	
	СтрокаТаблицы.НастройкиОтправкиПредставление = СтрСоединить(СтрокиНастроек, НСтр("ru = ', '"));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакВыгружатьДополнительныеСведения() 
	
	ТаблицаНстроек = ИсходящиеДокументы.Выгрузить();
	ТаблицаНстроек.Свернуть("ИдентификаторПолучателя");
	ИдентификаторыПолучателя = ТаблицаНстроек.ВыгрузитьКолонку("ИдентификаторПолучателя");
	
	ДанныеОператоровКонтрагента = Новый Соответствие;
	
	Для каждого СтрокаНастроек Из ИсходящиеДокументы Цикл
		ДанныеОператораКонтрагента = ДанныеОператоровКонтрагента[СтрокаНастроек.ИдентификаторПолучателя];
		Если ДанныеОператораКонтрагента = Неопределено Тогда
			ДанныеОператораКонтрагента = РегистрыСведений.ОператорыЭДО.ИнформацияОбОператореЭДОУчетнойЗаписи(СтрокаНастроек.ИдентификаторПолучателя);
			Если ДанныеОператораКонтрагента = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ДанныеОператоровКонтрагента.Вставить(СтрокаНастроек.ИдентификаторПолучателя, ДанныеОператораКонтрагента);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДанныеОператораКонтрагента) Тогда
			СтрокаНастроек.ВыгружатьДополнительныеСведения = ДанныеОператораКонтрагента.ОтправлятьДополнительныеСведения;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область УдалениеНастройкиОбмена

&НаКлиенте
Процедура УдалитьНастройкуОтправки()
	
	ВидыДокументов = Неопределено;
	Если КлючНастройкиОтправкиПустой()
		ИЛИ ЗначениеЗаполнено(КлючНастройкиОтправки.Договор) Тогда
		ТекстВопроса = НСтр("ru = 'Удалить настройку обмена с контрагентом?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Настройка обмена с контрагентом будет удалена. Отправка электронных документов контрагенту будет невозможна.
			|Продолжить?'");
		ВидыДокументов = ВидыДокументовДляРазрываПриглашенийПередУдалением(КлючНастройкиОтправки);
	КонецЕсли;
	
	Если ВидыДокументов = Неопределено Тогда
		Описание = Новый ОписаниеОповещения("УдалитьНастройкуОтправкиПослеВопроса", ЭтотОбъект);
		ПоказатьВопрос(Описание, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ОчиститьСообщения();
		ТекстСообщения = НСтр("ru = 'Для удаления настройки необходимо вначале разорвать приглашение.'");
		
		Для Каждого СтрокаКоллекции Из ИсходящиеДокументы Цикл
			
			Если ЗначениеЗаполнено(СтрокаКоллекции.ПрикладнойВидЭД) Тогда
				ВидДокумента = СтрокаКоллекции.ПрикладнойВидЭД;
			Иначе
				ВидДокумента = СтрокаКоллекции.ВидДокумента;
			КонецЕсли;
			
			Если ВидыДокументов.Найти(ВидДокумента) <> Неопределено Тогда
				
				РеквизитФормы = "СтатусПриглашения";
				
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , РеквизитФормы);
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидыДокументовДляРазрываПриглашенийПередУдалением(Знач КлючНастройкиОтправки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ВидДокумента КАК ВидДокумента,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ПрикладнойВидЭД КАК ПрикладнойВидДокумента
		|ИЗ
		|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК НастройкиОтправкиЭлектронныхДокументовПоВидам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
		|		ПО НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторОтправителя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации
		|			И НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторПолучателя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента
		|			И (ПриглашенияКОбменуЭлектроннымиДокументами.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПриглашений.Принято))
		|ГДЕ
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель = &Отправитель
		|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель = &Получатель
		|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Договор = &Договор";
	
	Запрос.УстановитьПараметр("Отправитель", КлючНастройкиОтправки.Отправитель);
	Запрос.УстановитьПараметр("Получатель", КлючНастройкиОтправки.Получатель);
	Запрос.УстановитьПараметр("Договор", КлючНастройкиОтправки.Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВидыДокументов = Новый Массив;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ВидДокумента = Перечисления.ВидыЭД.ПрикладнойЭД Тогда
			ВидыДокументов.Добавить(Выборка.ПрикладнойВидДокумента);
		Иначе
			ВидыДокументов.Добавить(Выборка.ВидДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВидыДокументов;
	
КонецФункции

&НаКлиенте
Процедура УдалитьНастройкуОтправкиПослеВопроса(Ответ, Контекст) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если КлючНастройкиОтправкиПустой() Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Организация"       , КлючНастройкиОтправки.Отправитель);
	ПараметрыПроцедуры.Вставить("Контрагент"        , КлючНастройкиОтправки.Получатель);
	ПараметрыПроцедуры.Вставить("ДоговорКонтрагента", КлючНастройкиОтправки.Договор);
	
	ДлительнаяОперация = НачатьУдалениеНастроекОтправкиНаСервере(УникальныйИдентификатор, ПараметрыПроцедуры);
	
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ТолькоПросмотр = Истина;
		Элементы.ГруппаОжиданиеУдаления.Видимость = Истина;
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		Описание = Новый ОписаниеОповещения("УдалитьНастройкуОтправкиЗавершение", ЭтаФорма);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Описание, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьУдалениеНастроекОтправкиНаСервере(Знач УникальныйИдентификатор, Знач ПараметрыПроцедуры)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Удаление настройки отправки электронных документов'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.УдалитьНастройкиОтправкиЭДО",
		ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура УдалитьНастройкуОтправкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТолькоПросмотр = Ложь;
	Элементы.ГруппаОжиданиеУдаления.Видимость = Ложь;

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		
		ТекстСообщения = НСтр("ru = 'Во время удаления настройки отправки произошла ошибка.'");
		
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(НСтр("ru = 'Удаление настройки отправки ЭДО'"),
			Результат.ПодробноеПредставлениеОшибки, ТекстСообщения);
			
		Возврат;
	КонецЕсли;
	
	Если ПолучитьИзВременногоХранилища(Результат.АдресРезультата) = Истина Тогда
		
		Оповестить("ОбновитьТекущиеДелаЭДО");
		Закрыть();
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Во время удаления настройки отправки произошла ошибка.
                               |Подробнее см. в журнале регистрации.'");
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
