#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет запись в регистр СостояниеДокументовПоЭДО.
//
// Параметры:
//  Документ - СправочникСсылка.ВнутренниеДокументы - Документ 1С:Документооборот.
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент, с которым ведется ЭДО.
//  Комментарий - Строка - Комментарий к состоянию.
//
Процедура Добавить(Документ, Контрагент, СостояниеВерсииДокументаПоЭДО, НаправлениеЭД = Неопределено, Комментарий = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостояниеДокументовПоЭДО");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ДокументДО", Документ);
		ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
		Блокировка.Заблокировать();
		
		Период = ТекущаяДатаСеанса();
		СостояниеДокумента = ПолучитьСостояниеДокумента(Документ, Период, Контрагент);
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = Период;
		МенеджерЗаписи.ДокументДО = Документ;
		МенеджерЗаписи.Контрагент = Контрагент;
		МенеджерЗаписи.СостояниеВерсииДокументаПоЭДО = СостояниеВерсииДокументаПоЭДО;
		Если Не Комментарий = Неопределено Тогда
			МенеджерЗаписи.Комментарий = Комментарий;
		КонецЕсли;
		МенеджерЗаписи.НаправлениеЭД = ?(НаправлениеЭД <> Неопределено,
			НаправлениеЭД,
			СостояниеДокумента.Направление);
		МенеджерЗаписи.Записать();
		
		ЗарегистрироватьИзменениеСостоянияДокументаПоЭДО(
			МенеджерЗаписи.ДокументДО,
			МенеджерЗаписи.Контрагент,
			МенеджерЗаписи.НаправлениеЭД,
			СостояниеДокумента.Состояние,
			МенеджерЗаписи.СостояниеВерсииДокументаПоЭДО);
		
		Делопроизводство.ЗаписатьДанныеДокумента(Документ, "СостояниеЭДО", СостояниеВерсииДокументаПоЭДО);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Удаляет записи из регистра СостояниеДокументовПоЭДО.
//
// Параметры:
//  Документ - СправочникСсылка.ВнутренниеДокументы - Документ 1С:Документооборот.
//
Процедура Удалить(Документ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостояниеДокументовПоЭДО");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ДокументДО", Документ);
		Блокировка.Заблокировать();
		
		СостояниеДокумента = ПолучитьСостояниеДокумента(Документ, Неопределено, Неопределено);
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументДО.Установить(Документ);
		НаборЗаписей.Записать();
		
		ЗарегистрироватьИзменениеСостоянияДокументаПоЭДО(
			Документ,
			СостояниеДокумента.Контрагент,
			СостояниеДокумента.Направление,
			СостояниеДокумента.Состояние,
			Перечисления.СостоянияВерсийЭДДО.ПустаяСсылка());
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Возвращает состояние документа в ЭДО.
//
// Параметры:
//  Документ   - СправочникСсылка.ВнутренниеДокументы, СправочникОбъект.ВнутренниеДокументы - 
//				 Документ 1С:Документооборот, для которого необходимо получить состояние.
//  Дата	   - Дата - Дата, на которую необходимо получить состояние.
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент, для которого необходимо получить состояние.
// 
// Возвращаемое значение:
//  Структура - 
//		* Дата - Дата - Дата установки состояния
//		* Состояние - ПеречислениеСсылка.СостоянияВерсийЭДДО - Состояние документа в ЭДО
//		* Направление - ПеречислениеСсылка.НаправленияЭД - Направление документа (входящий / исходящий)
//		* Комментарий - Строка - Комментарий
//
Функция ПолучитьСостояниеДокумента(Документ, Дата = Неопределено, Контрагент = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВозвращаемоеЗначение = Новый Структура("Дата, Состояние, Направление, Комментарий, Контрагент",
		Дата(1, 1, 1),
		Перечисления.СостоянияВерсийЭДДО.ПустаяСсылка(),
		Перечисления.НаправленияЭД.ПустаяСсылка(),
		"",
		Справочники.Контрагенты.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрШаблон(
		"ВЫБРАТЬ
		|	СостояниеДокументовПоЭДО.Период КАК Дата,
		|	СостояниеДокументовПоЭДО.СостояниеВерсииДокументаПоЭДО КАК Состояние,
		|	СостояниеДокументовПоЭДО.Контрагент КАК Контрагент,
		|	СостояниеДокументовПоЭДО.НаправлениеЭД КАК Направление,
		|	СостояниеДокументовПоЭДО.Комментарий КАК Комментарий
		|ИЗ
		|	РегистрСведений.СостояниеДокументовПоЭДО.СрезПоследних(
		|			&Дата,
		|			ДокументДО = &Документ
		|				%1) КАК СостояниеДокументовПоЭДО",
		?(ЗначениеЗаполнено(Контрагент), "И Контрагент = &Контрагент", ""));
	
	Запрос.УстановитьПараметр("Дата", ?(Не ЗначениеЗаполнено(Дата), ТекущаяДатаСеанса(), Дата));
	Запрос.УстановитьПараметр("Документ", ?(ОбщегоНазначения.ЗначениеСсылочногоТипа(Документ), Документ, Документ.Ссылка));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение, Выборка);
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает состояние версии документа ДО по ЭДО.
//
// Параметры:
//  Документ - СправочникСсылка.ВнутренниеДокументы, СправочникОбъект.ВнутренниеДокументы - 
//			   Документ, для которого необходимо получить состояние.
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент по ЭДО.
// 
// Возвращаемое значение:
//  СостоянияВерсийЭДДО - ПеречислениеСсылка.СостоянияВерсийЭДДО - Cостояние версии документа ДО по ЭДО.
//
Функция ДанныеСостоянияДокументаПоЭДО(Документ, Контрагент = Неопределено, НаДату = Неопределено, НаправлениеЭД = Неопределено) Экспорт
	
	СостояниеДокумента = ПолучитьСостояниеДокумента(Документ, НаДату, Контрагент);	
	
	НаДату = СостояниеДокумента.Дата;
	НаправлениеЭД = СостояниеДокумента.Направление;
	
	Возврат СостояниеДокумента.Состояние;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Регистрирует бизнес-событие "Изменение состояния документа по ЭДО".
//
// Параметры:
//  Документ           - СправочникСсылка.ВнутренниеДокументы   - Документ 1С:Документооборот.
//  Контрагент         - СправочникСсылка.Контрагенты           - Контрагент, с которым ведется ЭДО.
//  НаправлениеЭД      - ПеречислениеСсылка.НаправленияЭД       - Направление электронного документа.
//  СтароеСостояниеЭДО - ПеречислениеСсылка.СостоянияВерсийЭДДО - Старое состояние ЭДО.
//  НовоеСостояниеЭДО  - ПеречислениеСсылка.СостоянияВерсийЭДДО - Новое состояние ЭДО.
//
Процедура ЗарегистрироватьИзменениеСостоянияДокументаПоЭДО(Документ, Контрагент, НаправлениеЭД, СтароеСостояниеЭДО, НовоеСостояниеЭДО)
	
	Если СтароеСостояниеЭДО = НовоеСостояниеЭДО Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеИзмененияСостоянияЭДО = Новый Структура("Контрагент, НаправлениеЭД, СтароеСостояниеЭДО, НовоеСостояниеЭДО");
	ДанныеИзмененияСостоянияЭДО.Контрагент = Контрагент;
	ДанныеИзмененияСостоянияЭДО.НаправлениеЭД = НаправлениеЭД;
	ДанныеИзмененияСостоянияЭДО.СтароеСостояниеЭДО = СтароеСостояниеЭДО;
	ДанныеИзмененияСостоянияЭДО.НовоеСостояниеЭДО = НовоеСостояниеЭДО;
	
	БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(
		Документ,
		Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаПоЭДО,
		Новый ХранилищеЗначения(ДанныеИзмененияСостоянияЭДО));
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли