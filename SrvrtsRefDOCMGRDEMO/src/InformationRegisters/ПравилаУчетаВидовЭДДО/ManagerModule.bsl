
#Область ПрограммныйИнтерфейс

// Получает параметры настройки соответствия видов ЭД и видов документов 1С:Документооборот
//
// Парамтеры:
//  ВидЭД - ПеречислениеСсылка.ВидыЭД - Вид электронного документа
//  Организация - СправочникСсылка.Организации - Организация, принимающая документ.
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент, с которым ведется ЭДО.
Функция ПараметрыДокументаПоВидуЭД(ВидЭД, Организация, Контрагент, ТипЭД = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидЭД", ВидЭД);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТипЭД", ТипЭД);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПравилаУчетаВидовЭДДО.ВидДокумента КАК ВидДокумента,
		|	ПравилаУчетаВидовЭДДО.Шаблон КАК Шаблон,
		|	ПравилаУчетаВидовЭДДО.Ответственный КАК Ответственный,
		|	ПравилаУчетаВидовЭДДО.Папка КАК Папка,
		|	ПравилаУчетаВидовЭДДО.ВопросДеятельности КАК ВопросДеятельности,
		|	ПравилаУчетаВидовЭДДО.ШаблонНаименования КАК ШаблонНаименования,
		|	ПравилаУчетаВидовЭДДО.ШаблонСодержания КАК ШаблонСодержания
		|ИЗ
		|	РегистрСведений.ПравилаУчетаВидовЭДДО КАК ПравилаУчетаВидовЭДДО
		|ГДЕ
		|	ПравилаУчетаВидовЭДДО.ВидЭД = &ВидЭД
		|	И ПравилаУчетаВидовЭДДО.Контрагент = &Контрагент
		|	И ПравилаУчетаВидовЭДДО.Организация = &Организация
		|	И ПравилаУчетаВидовЭДДО.Принимать";
	
	Если ЗначениеЗаполнено(ТипЭД) Тогда
		ТекстЗапроса = ТекстЗапроса + " И ПравилаУчетаВидовЭДДО.ТипЭД = &ТипЭД";
	КонецЕсли;
	
	ПараметрыДокументаПоВидуЭД = Новый Структура;
	ПараметрыДокументаПоВидуЭД.Вставить("ВидДокумента");
	ПараметрыДокументаПоВидуЭД.Вставить("Шаблон");
	ПараметрыДокументаПоВидуЭД.Вставить("Ответственный");
	ПараметрыДокументаПоВидуЭД.Вставить("Папка");
	ПараметрыДокументаПоВидуЭД.Вставить("ВопросДеятельности");
	ПараметрыДокументаПоВидуЭД.Вставить("ШаблонНаименования");
	ПараметрыДокументаПоВидуЭД.Вставить("ШаблонСодержания");
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыДокументаПоВидуЭД, Выборка);
	КонецЕсли;
	
	Возврат ПараметрыДокументаПоВидуЭД;
	
КонецФункции

Функция НастройкиПриемаДокументов(Организация, Контрагент) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОписаниеТипаБулево = Новый ОписаниеТипов("Булево");
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка");
	
	ВходящиеДокументы = Новый ТаблицаЗначений;
	
	ВходящиеДокументы.Колонки.Добавить("Организация"        , Метаданные.ОпределяемыеТипы.Организация.Тип);
	ВходящиеДокументы.Колонки.Добавить("Контрагент"         , Метаданные.ОпределяемыеТипы.УчастникЭДО.Тип);
	ВходящиеДокументы.Колонки.Добавить("ВидЭД"              , Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЭД"));
	ВходящиеДокументы.Колонки.Добавить("ТипЭД"              , Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЭД"));
	
	ВходящиеДокументы.Колонки.Добавить("Принимать"          , ОписаниеТипаБулево);
	ВходящиеДокументы.Колонки.Добавить("ВидДокумента"       , Новый ОписаниеТипов("СправочникСсылка.ВидыВнутреннихДокументов"));
	ВходящиеДокументы.Колонки.Добавить("ВопросДеятельности" , Новый ОписаниеТипов("СправочникСсылка.ВопросыДеятельности"));
	ВходящиеДокументы.Колонки.Добавить("Ответственный"      , Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ВходящиеДокументы.Колонки.Добавить("Папка"              , Новый ОписаниеТипов("СправочникСсылка.ПапкиВнутреннихДокументов"));
	ВходящиеДокументы.Колонки.Добавить("Шаблон"             , Новый ОписаниеТипов("СправочникСсылка.ШаблоныВнутреннихДокументов"));
	ВходящиеДокументы.Колонки.Добавить("ШаблонНаименования" , ОписаниеТипаСтрока);
	ВходящиеДокументы.Колонки.Добавить("ШаблонСодержания"   , ОписаниеТипаСтрока);
	
	АктуальныеВидыЭД = Новый Соответствие;
	ОбменСКонтрагентамиПереопределяемый.ПолучитьАктуальныеВидыЭД(АктуальныеВидыЭД);
	
	НастройкаПоУмолчанию = Новый Структура;
	НастройкаПоУмолчанию.Вставить("Организация"        , Организация);
	НастройкаПоУмолчанию.Вставить("Контрагент"         , Контрагент);
	НастройкаПоУмолчанию.Вставить("Принимать"          , Ложь);
	НастройкаПоУмолчанию.Вставить("ВидДокумента"       , Неопределено);
	НастройкаПоУмолчанию.Вставить("ВопросДеятельности" , Неопределено);
	НастройкаПоУмолчанию.Вставить("Ответственный"      , Неопределено);
	НастройкаПоУмолчанию.Вставить("Шаблон"             , Неопределено);
	НастройкаПоУмолчанию.Вставить("ШаблонНаименования" , "");
	НастройкаПоУмолчанию.Вставить("ШаблонСодержания"   , "");
	
	Для Каждого Элемент Из АктуальныеВидыЭД Цикл
		
		ВидЭД = Элемент.Ключ;
		
		Если ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ ВидЭД = Перечисления.ВидыЭД.ВозвратТоваровМеждуОрганизациями 
			ИЛИ ВидЭД = Перечисления.ВидыЭД.АктЗаказчик 
			ИЛИ ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель Тогда
			
			Продолжить;
		КонецЕсли;
		
		Если ВидЭД <> Перечисления.ВидыЭД.ПроизвольныйЭД Тогда
			
			НоваяСтрока = ВходящиеДокументы.Добавить();
			НоваяСтрока.ВидЭД = ВидЭД;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НастройкаПоУмолчанию);
			
		Иначе
			Для Каждого ТипЭД Из Перечисления.ТипыЭД Цикл
				
				НоваяСтрока = ВходящиеДокументы.Добавить();
				НоваяСтрока.ВидЭД = ВидЭД;
				НоваяСтрока.ТипЭД = ТипЭД;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НастройкаПоУмолчанию);
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПравилаУчетаВидовЭДДО.ВидЭД КАК ВидЭД,
		|	ПравилаУчетаВидовЭДДО.ТипЭД КАК ТипЭД,
		|	ПравилаУчетаВидовЭДДО.ВидДокумента КАК ВидДокумента,
		|	ПравилаУчетаВидовЭДДО.ВопросДеятельности КАК ВопросДеятельности,
		|	ПравилаУчетаВидовЭДДО.Ответственный КАК Ответственный,
		|	ПравилаУчетаВидовЭДДО.Папка КАК Папка,
		|	ПравилаУчетаВидовЭДДО.Шаблон КАК Шаблон,
		|	ПравилаУчетаВидовЭДДО.ШаблонНаименования КАК ШаблонНаименования,
		|	ПравилаУчетаВидовЭДДО.ШаблонСодержания КАК ШаблонСодержания,
		|	ПравилаУчетаВидовЭДДО.Принимать КАК Принимать
		|ИЗ
		|	РегистрСведений.ПравилаУчетаВидовЭДДО КАК ПравилаУчетаВидовЭДДО
		|ГДЕ
		|	ПравилаУчетаВидовЭДДО.Организация = &Организация
		|	И ПравилаУчетаВидовЭДДО.Контрагент = &Контрагент";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	ВыборкаЗаписанных = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗаписанных.Следующий() Цикл
		
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("ВидЭД", ВыборкаЗаписанных.ВидЭД);
		ОтборСтрок.Вставить("ТипЭД", ВыборкаЗаписанных.ТипЭД);
		
		СтрокиНастроек = ВходящиеДокументы.НайтиСтроки(ОтборСтрок);
		
		Для Каждого Строка Из СтрокиНастроек Цикл
			ЗаполнитьЗначенияСвойств(Строка, ВыборкаЗаписанных);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ВходящиеДокументы;
	
КонецФункции

#КонецОбласти