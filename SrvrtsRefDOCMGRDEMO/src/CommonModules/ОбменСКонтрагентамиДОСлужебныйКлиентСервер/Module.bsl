 
#Область ПрограммыныйИнтерфейс

// Формирует представление результата создания документов ДО по входящим ЭД
//
// Параметры:
//	КСозданию -- Число -- Сколько документов было к созданию
//	Создано -- Число -- Сколько из них было создано
//	СозданоСОшибками -- Число -- Сколько из них было создано с ошибками
//
// Возвращаемое значение:
//	Строка -- Представление результата формирования
Функция ОписаниеРезультатаСозданияДокументовДОПоВходящимЭД(КСозданию, Создано, СозданоСОшибками) Экспорт
	
	ТекстРезультата = НСтр("ru = 'Нет входящих электронных документов к отражению в учете.'");
	
	Если КСозданию <= 0 Тогда
		Возврат ТекстРезультата;
	КонецЕсли;
	
	Если Создано <= 0 Тогда
		ТекстРезультата = СтрШаблон(
			НСтр("ru = 'Не удалось отразить в учете электронных документов: %1. Подробности в журнале регистрации'"),
			КСозданию);
		
		Возврат ТекстРезультата;
	КонецЕсли;
	
	Если КСозданию = Создано Тогда
		
		Если СозданоСОшибками <= 0 Тогда
		
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Создано внутренних документов: %1'"),
				Создано);
			
		Иначе
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Создано внутренних документов: %1, из них с ошибками: %2'"),
				Создано,
				СозданоСОшибками);
		
		КонецЕсли;
		
	Иначе
		
		Если СозданоСОшибками <= 0 Тогда
		
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Создано внутренних документов: %1 из %2'"),
				Создано,
				КСозданию);
			
		Иначе
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Создано внутренних документов: %1 из %2, из них с ошибками %3'"),
				Создано,
				КСозданию,
				СозданоСОшибками);
			
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат ТекстРезультата;
	
КонецФункции // ()

// Формирует представление результата создания ЭД по документам ДО
//
// Параметры:
//	КСозданию -- Число -- Сколько документов нужно создать
//	Создано -- Число -- Сколько из них было создано
//	СозданоСОшибками -- Число -- Сколько удалось подготовить к отправке
//	Отправлено -- Число -- Сколько удалось отправить
//	НемедленнаяОтправка -- Булево -- Указывает, производилась ли немедленная отправка ЭД
//
// Возвращаемое значение:
//	Строка -- Представление результата формирования
Функция ОписаниеРезультатаСозданияЭДПоДокументамДО(КСозданию, Создано, КОтправке, Отправлено, НемедленнаяОтправка) Экспорт
	
	ТекстРезультата = НСтр("ru = 'Нет документов 1С:Документооборот к отправке.'");
	
	Если КСозданию <= 0 Тогда
		Возврат ТекстРезультата;
	КонецЕсли;
	
	Если Создано <= 0 Тогда
		ТекстРезультата = СтрШаблон(
			НСтр("ru = 'Не удалось отразить создать электронных документов: %1. Подробности смотрите в журнале регистрации'"),
			КСозданию);
		
		Возврат ТекстРезультата;
	КонецЕсли;
	
	Если КСозданию = Создано
		И Создано = КОтправке Тогда
		
		Если Не НемедленнаяОтправка Тогда
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Успешно создано и подготовлено к отправке электронных документов: %1'"),
				Создано);
			
		ИначеЕсли КОтправке = Отправлено Тогда
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Успешно создано и отправлено электронных документов: %1'"),
				Создано);
			
		Иначе
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Успешно создано и подготовлено к отправке электронных документов: %1, из них отправлено: %2'"),
				Создано,
				Отправлено);
			
		КонецЕсли;
		
	ИначеЕсли КСозданию = Создано Тогда
		
		Если Не НемедленнаяОтправка Тогда
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Успешно создано электронных документов: %1, из них подготовлено к отправке: %2'"),
				Создано,
				КОтправке);
				
		ИначеЕсли КОтправке = Отправлено Тогда
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Успешно создано документов: %1, из них отправлено: %2'"),
				Создано,
				Отправлено);
			
		Иначе
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Успешно создано электронных документов: %1, из них подготовлено к отправке: %2, отправлено: %3'"),
				Создано,
				КОтправке,
				Отправлено);
			
		КонецЕсли;
		
	Иначе
		
		Если Не НемедленнаяОтправка Тогда
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Успешно создано электронных документов: %1 из %2, подготовлено к отправке: %3'"),
				Создано,
				КСозданию,
				КОтправке);
				
		ИначеЕсли КОтправке = Отправлено Тогда
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Успешно создано документов: %1 из %2, отправлено: %3'"),
				Создано,
				КСозданию,
				Отправлено);
			
		Иначе
			
			ТекстРезультата = СтрШаблон(
				НСтр("ru = 'Успешно создано электронных документов: %1 из %2, подготовлено к отправке: %3, отправлено: %4'"),
				Создано,
				КСозданию,
				КОтправке,
				Отправлено);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстРезультата;
	
КонецФункции // ()

#КонецОбласти
